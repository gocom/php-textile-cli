#compdef textile

# PHP-Textile CLI
# https://github.com/gocom/php-textile-cli
#
# Copyright (C) 2025 Jukka Svahn
#
# Permission is hereby granted, free of charge, to any person obtaining a
# copy of this software and associated documentation files (the "Software"),
# to deal in the Software without restriction, including without limitation
# the rights to use, copy, modify, merge, publish, distribute, sublicense,
# and/or sell copies of the Software, and to permit persons to whom the
# Software is furnished to do so, subject to the following conditions:
#
# The above copyright notice and this permission notice shall be included in
# all copies or substantial portions of the Software.
#
# THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
# IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
# FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
# AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
# LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
# OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
# SOFTWARE.

local curcontext=$curcontext state document_types link_relationships urls

document_types=(
    'html5: Output HTML5 document'
    'xhtml: Output XHTML document'
)

link_relationships=(
    'nofollow: Add rel nofollow to links'
    'noreferrer: Add rel noreferrer to links'
)

urls=(
    'http://'
    'https://'
)

_arguments -A '-*' \
    '(- *)'{-h,--help}'[Print help]: :->noargs' \
    '(-V --version)'{-V,--version}'[Print version]: :->noargs' \
    '(-t --document-type=)'{-t,--document-type=}'[Document type]: :->document_types' \
    '(-d --document-root-directory=)'{-d,--document-root-directory=}'[Set document root directory]: :_directories' \
    '(-l --lite)'{-l,--lite}'[Enable lite mode]' \
    '(-i --no-images)'{-i,--no-images}'[Disable images]' \
    '(-L --link-relationship=)'{-L,--link-relationship=}'[Set link relationship]: :->link_relationships' \
    '(-r --restricted)'{-r,--restricted}'[Enable restricted mode]' \
    '(-U --raw-blocks)'{-U,--raw-blocks}'[Enable raw blocks]' \
    '(-A --align-classes)'{-A,--align-classes}'[Enable alignment classes]' \
    '(-a --no-align-classes)'{-a,--no-align-classes}'[Disable alignment classes]' \
    '(-b --no-block-tags)'{-b,--no-block-tags}'[Disable block tags]' \
    '(-w --no-line-wrap)'{-w,--no-line-wrap}'[Disable line wrapping]' \
    '(-p --image-prefix=)'{-p,--image-prefix=}'[Set image URL prefix]: :->urls' \
    '(-P --link-prefix)'{-P,--link-prefix=}'[Set link URL prefix]: :->urls' \
    '(-z --no-dimensions)'{-z,--no-dimensions}'[Disable adding width and height to images]' \
    '(-o --output=)'{-o,--output=}'[Save HTML file as]: :_files' \
    '(-q --quiet)'{-q,--quiet}'[Do not output any message]' \
    '(--ansi)--ansi[Force enable ANSI output]' \
    '(--no-ansi)--no-ansi[Disable ANSI output]' \
    '(--no-interaction)--no-interaction[Do not ask any interactive question]' \
    '(-v -vv -vvv --verbose)'{-v,-vv,-vvv,--verbose}'[Increase verbosity: 1 for verbose, 2 for more verbose and 3 for debug]' \
    :INPUT:_files

if [[ CURRENT -ge 1 ]]; then
    case $state in
        document_types)
            _describe -t commands "Document types" document_types
            ;;
        link_relationships)
            _describe -t commands "Link relationships" link_relationships
            ;;
        noargs)
            _message "Nothing to complete"
            ;;
        urls)
            _describe -t commands "URL" urls
            ;;
    esac

    return
fi
